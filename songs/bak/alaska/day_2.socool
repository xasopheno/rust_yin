import songs/std.socool

{ f: 340, l: 11/10, g: 1, p: 0 }

overtones = {
    O[
    	(11/4, 0, 1/70, 1/5),
    	(11/4, 0, 1/70, 1/5),
    	(4/1, 0, 1/70, -1/6),
    	(2, -3, 1, 1/6),
    	(2, 0, 1, -1/7),
    	(3/2, -3, 1, 1/7),
    	(3/2, 0, 1, -1/8),
    	(1/1, 1, 4/5, 1/8),
      	(1/1, 0, 4/5, -1/9),
      	(1/2, 0, 1, 1/9),
      	(1/4, 0, 1, -1/9),
      	(1/4, 1, 1, 1/9),
  	]
}

bass = {
	overtones |
    Seq [
        3/2^1/64 | Gain 1/2, 11/8^1/32 | Gain 1/2, 
        1^10/9, 0^1/5
    ]
	| Sequence [
        0^1, 1^1, 1^1, 6/5^1, 6/5^1, 1^1, 1^1, 
        7/8^2.2, 7/8^1, 7/8^1, 3/4^1, 3/4^1, 
        7/8^1, 7/8^1, 1/1^1,

        0^1, 1^1, 1^1, 6/5^8/7, 6/5^6/7, 1^1, 1^1, 
        4/3^2, 7/8^1, 7/8^1, 
        6/5^1, 6/5^1, 3/4^1, 9/8^1,
        1^8/10,

	] 
    | Length 1/5
}

bubble = {
    Seq [
        0^5, 1^2, 0^10
    ]    
    | Tm 2
    | Seq [
        O[
            (3, 3, 1/4, 5/6),
            (3, 0, 1/4, -5/6),
            (12/5, 0, 1, 5/6),
            (2/1, 0, 1, -5/6),
            (3/2, -3, 1, 5/6),
            (3/2, 0, 1, -5/6),
            (6/5, 0, 1, 5/6),
            (1/1, 5, 1, -5/6),
            (1/1, 0, 1, 5/6),
            (3/4, 0, 1, -5/6),
            (1/2, 0, 1, 5/6),
            (1/2, 2, 1, -5/6),
        ]
    ]
    | Seq [
        Tm 3/2,
        AsIs, 
        Seq [0^1/2, Repeat 2] > FitLength AsIs,
        Tm 9/8, Tm 1,
        Tm 9/8,
        Tm 1 | Seq [0^1/2, Repeat 2] > FitLength AsIs,
        Tm 3/4, 
    ]
}

melody = {
    O[
    	(2, 13, 1, 1/2),
    	(2, -1, 1, -1/2),
    	(3/2, -5, 1, 1/2),
      	(3/2, 0, 1, -1/2),
    	(1/1, 1, 1, 1/2),
      	(1/1, 0, 1, -1/2),
      	(1/2, 0, 1, 0),
  	]
    | Tm 2
    | Seq [
        0^1, 6/5^3, 9/8^1/3, 1^2, 9/8^1, 1^3
    ]
    > FitLength bass
}

shine = {
    O[
    	(1/1, 1/7, 1, -1/2),
      	(1/1, 0, 1, 1/2),
  	]
    | Tm 3
    | Seq [
        Tm 3/2, Tm 2, Tm 6/5, 
        Tm 9/8, Tm 3/2, Tm 1,
        Tm 9/8, Tm 5/4, Tm 11/8, Tm 3/2
    ]
    | Overlay [
        AsIs, 
        Tm 3/2 
        | Repeat 3 
        | Length 1/3,
        Gain 1/4
        | Tm 9/4 
        | Repeat 5
        | Length 1/5
    ]
    | Seq [
        0^1,
        1^1,
        1^1/2,
    ]
    | ModulateBy [
        std.fade_in, 
        std.fade_out 
    ]
    | ModulateBy [
        std.melt
        | Seq [
            AsIs, Ta -20, Ta -40, Ta -60, 
            Ta -80, Ta -100, Ta -120, Ta -140
        ]
    ]
    | ModulateBy [
        Seq [
            PanA 1/2, PanA 0, PanA -1/2, PanA -1, 
            PanA -1/2, PanA 0, PanA 1/2, PanA 1
        ] | Repeat 3
    ]
    > FitLength bass
}

main = {
    Seq [
        Overlay [
            Gain 1/4 | shine,
            Gain 1 | melody,
            bass,
            Gain 4/5 | Seq [bubble]
            > FitLength bass
        
        ]
        | Repeat 2
        | Seq [AsIs, Tm 7/8, AsIs]
    
    ]
}
	
